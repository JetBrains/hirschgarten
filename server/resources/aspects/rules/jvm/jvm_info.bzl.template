load("//${bspPath}/aspects:utils/utils.bzl", "create_struct", "file_location", "do_starlark_string_expansion")

def extract_jvm_info(target, ctx, output_groups, dep_targets, **kwargs):
    raw_jvm_flags = getattr(ctx.rule.attr, "jvm_flags", [])
    data_deps = getattr(ctx.rule.attr, "data", [])
    jvm_flags = do_starlark_string_expansion(ctx, "jvm_flags", raw_jvm_flags, data_deps)
    args = getattr(ctx.rule.attr, "args", [])
    main_class = getattr(ctx.rule.attr, "main_class", None)

    resource_strip_prefix_attr = getattr(ctx.rule.attr, "resource_strip_prefix", None)
    resource_strip_prefix = None
    if type(resource_strip_prefix_attr) == "string":
        resource_strip_prefix = resource_strip_prefix_attr
    elif type(resource_strip_prefix_attr) == "Target":
        # special case for rules_kotlin
        # Label("some/package") -> "some/package" (assuming there's no target <relative path>/some/package:package)
        resource_strip_prefix = resource_strip_prefix_attr.label.name
    info = create_struct(
        jvm_flags = jvm_flags,
        main_class = main_class,
        args = args,
        resource_strip_prefix = resource_strip_prefix,
    )

    return dict(jvm_target_info = info), None
