load("//${bspPath}/aspects:utils/utils.bzl", "IMPORT_RULE_KIND", "create_struct", "file_location", "is_external", "update_sync_output_groups", "filter_not_none", "do_starlark_string_expansion", "map_with_resolve_files")
load("//${bspPath}/aspects:utils/jvm_common.bzl", "to_generated_jvm_outputs", "get_generated_jars", "to_jvm_outputs", "provider_resolve_depsets")
load("//${bspPath}/aspects:rules/java/java_info.bzl", "get_java_plugin_info", "get_javacopts")

#if( $kotlinEnabled == "true" )
load("//${bspPath}/aspects:rules/kt/kt_info.bzl", "get_kt_jvm_provider", "get_additional_javac_options", "get_kotlin_plugins")
#else
def get_kt_jvm_provider(target):
    return None
def get_additional_javac_options(ctx, target):
    return []
def get_kotlin_plugins(target, ctx, dep_targets):
    return []
#end

def get_jvm_provider(target):
    if hasattr(target, "scala"):
        return target.scala
    kt_jvm_provider = get_kt_jvm_provider(target)
    if kt_jvm_provider != None and hasattr(kt_jvm_provider, "outputs"):
        return kt_jvm_provider
    return None

def get_javac_opts(target, ctx):
    return get_javacopts(target, ctx) + get_additional_javac_options(ctx, target)

def has_api_generating_plugins(target, ctx, dep_targets, provider):
    if hasattr(provider, "api_generating_plugins"):
        # rules_java calculates api_generating_plugins for us already
        return len(provider.api_generating_plugins.processor_classes.to_list()) > 0

    plugins = get_kotlin_plugins(target, ctx, dep_targets)
    for plugin in plugins:
        java_plugin_info = get_java_plugin_info(plugin)
        if not java_plugin_info:
            continue
        if len(java_plugin_info.api_generating_plugins.processor_classes.to_list()) > 0:
            return True
    return False

def extract_jvm_info(target, ctx, output_groups, dep_targets, **kwargs):
    provider = get_jvm_provider(target)

    java_outputs = []
    if provider:
        if hasattr(provider, "java_outputs") and provider.java_outputs:
            java_outputs = provider.java_outputs
        elif hasattr(provider, "outputs") and provider.outputs:
            java_outputs = provider.outputs.jars

    jars, resolve_files_jars = map_with_resolve_files(to_jvm_outputs, java_outputs)

    generated_jars, resolve_files_generated_jars = get_generated_jars(provider)

    javac_opts = get_javac_opts(target, ctx)
    raw_jvm_flags = getattr(ctx.rule.attr, "jvm_flags", [])
    data_deps = getattr(ctx.rule.attr, "data", [])
    jvm_flags = do_starlark_string_expansion(ctx, "jvm_flags", raw_jvm_flags, data_deps)
    args = getattr(ctx.rule.attr, "args", [])
    main_class = getattr(ctx.rule.attr, "main_class", None)
    resolve_files = resolve_files_jars + resolve_files_generated_jars
    resolve_files_transitive = []
    if is_external(target) or ctx.rule.kind in IMPORT_RULE_KIND:
        resolve_files_transitive += provider_resolve_depsets(target, provider)
        update_sync_output_groups(output_groups, "bsp-sync-artifact", depset(resolve_files, transitive=resolve_files_transitive))
    else:
        update_sync_output_groups(output_groups, "bsp-build-artifact", depset(resolve_files))

    resource_strip_prefix_attr = getattr(ctx.rule.attr, "resource_strip_prefix", None)
    resource_strip_prefix = None
    if type(resource_strip_prefix_attr) == "string":
        resource_strip_prefix = resource_strip_prefix_attr
    elif type(resource_strip_prefix_attr) == "Target":
        # special case for rules_kotlin
        # Label("some/package") -> "some/package" (assuming there's no target <relative path>/some/package:package)
        resource_strip_prefix = resource_strip_prefix_attr.label.name
    info = create_struct(
        javac_opts = javac_opts,
        jvm_flags = jvm_flags,
        main_class = main_class,
        args = args,
        has_api_generating_plugins = has_api_generating_plugins(target, ctx, dep_targets, provider),
        resource_strip_prefix = resource_strip_prefix,
    )
    result = dict(jvm_target_info = info, jars = jars, generated_jars = generated_jars)
    if provider:
        result["jvm_target"] = True

    return result, None
