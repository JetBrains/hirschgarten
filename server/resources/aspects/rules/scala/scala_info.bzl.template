load("//${bspPath}/aspects:utils/utils.bzl", "file_location", "is_external", "map", "update_sync_output_groups", "map_with_resolve_files", "IMPORT_RULE_KIND")
load("//${bspPath}/aspects:utils/jvm_common.bzl", "to_generated_jvm_outputs", "get_generated_jars", "to_jvm_outputs", "provider_resolve_depsets")


SCALA_COMPILER_NAMES = [
    "scala3-compiler",
    "scala-compiler",
]

SCALA_LIBRARY_NAMES = [
    "scala3-interfaces",
    "scala3-library",
    "scala3-reflect",
    "scala-asm",
    "scala-library",
    "scala-reflect",
    "tasty-core",
    "compiler-interface",
]

def contains_substring(strings, name):
    for s in strings:
        if s in name:
            return True
    return False

def find_scalac_classpath(runfiles):
    result = []
    found_scala_compiler_jar = False
    for file in runfiles:
        name = file.basename
        if file.extension == "jar" and contains_substring(SCALA_COMPILER_NAMES, name):
            found_scala_compiler_jar = True
            result.append(file)
        elif file.extension == "jar" and contains_substring(SCALA_LIBRARY_NAMES, name):
            result.append(file)
    return result if found_scala_compiler_jar and len(result) >= 2 else []

def extract_scalatest_classpath_targets(rule_attr):
    def extract_from_attr(attr):
        attr_value = getattr(rule_attr, attr, None)
        if attr_value != None:
            return [str(attr_value.label)]
        return []

    return (
        extract_from_attr("_scalatest") +
        extract_from_attr("_scalatest_runner") +
        extract_from_attr("_scalatest_reporter")
    )

def extract_scala_info(target, ctx, output_groups, **kwargs):
    kind = ctx.rule.kind
    if not kind.startswith("scala_") and not kind.startswith("thrift_"):
        return None, None

    SCALA_TOOLCHAIN = "@${rulesetName}//scala:toolchain_type"

    scala_info = {}

    rule_attr = ctx.rule.attr

    # check of _scala_toolchain is necessary, because SCALA_TOOLCHAIN will always be present
    if hasattr(rule_attr, "_scala_toolchain"):
        common_scalac_opts = ctx.toolchains[SCALA_TOOLCHAIN].scalacopts
        if hasattr(rule_attr, "_scalac"):
            scalac = rule_attr._scalac
            compiler_classpath = find_scalac_classpath(scalac.default_runfiles.files.to_list())
            if compiler_classpath:
                scala_info["compiler_classpath"] = map(file_location, compiler_classpath)
                if is_external(scalac):
                    update_sync_output_groups(output_groups, "bsp-sync-artifact", depset(compiler_classpath))
    else:
        common_scalac_opts = []
    scala_info["scalac_opts"] = common_scalac_opts + getattr(ctx.rule.attr, "scalacopts", [])
    scala_info["scalatest_classpath_targets"] = extract_scalatest_classpath_targets(rule_attr)

    result = dict(scala_target_info = struct(**scala_info)) 

    provider = None
    if hasattr(target, "scala"):
        provider = target.scala
   
    java_outputs = []
    if provider:
        if hasattr(provider, "java_outputs") and provider.java_outputs:
            java_outputs = provider.java_outputs
        elif hasattr(provider, "outputs") and provider.outputs:
            java_outputs = provider.outputs.jars

    jars, resolve_files_jars = map_with_resolve_files(to_jvm_outputs, java_outputs)
    generated_jars, resolve_files_generated_jars = get_generated_jars(provider)
    result["java_common"] = dict(jars = jars, generated_jars = generated_jars)

    resolve_files = resolve_files_jars + resolve_files_generated_jars
    resolve_files_transitive = []
    if is_external(target) or ctx.rule.kind in IMPORT_RULE_KIND:
        resolve_files_transitive += provider_resolve_depsets(target, provider)
        update_sync_output_groups(output_groups, "bsp-sync-artifact", depset(resolve_files, transitive=resolve_files_transitive))
    else:
        update_sync_output_groups(output_groups, "bsp-build-artifact", depset(resolve_files))


    if provider and hasattr(provider, "api_generating_plugins") and (len(provider.api_generating_plugins.processor_classes.to_list()) > 0):
        result["has_api_generating_plugins"] = True

    return result, None
