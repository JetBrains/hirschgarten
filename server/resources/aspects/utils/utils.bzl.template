# Copyright 2019-2024 JetBrains s.r.o. and contributors. Use of this source code is governed by the Apache 2.0 license.

load("//${bspPath}/aspects:utils/make_variables.bzl", _expand_make_variables = "expand_make_variables")

def abs(num):
    if num < 0:
        return -num
    else:
        return num

def map(f, xs):
    return [f(x) for x in xs]

def filter(f, xs):
    return [x for x in xs if f(x)]

def not_none(x):
    return x != None

def filter_not_none(xs):
    return filter(not_none, xs)

def flatten(xss):
    return [x for xs in xss for x in xs]

# Provider used by the bsp_target_info_aspect. Holds aspect data returned from
# `core.bzl`.
BspAspectInfo = provider(
    fields = ["bsp_info"],
)

def flatmap(f, xs):
    return flatten(map(f, xs))



# Explanation: external paths usually start with `external/`, but if `--experimental_sibling_repository_layout`
# is enabled, they start with `../`.
def is_external_path(path):
    return path.startswith("../") or path.startswith("external/")

def file_location(file):
    if file == None:
        return None

    return to_file_location(
        file.path,
        file.root.path if not file.is_source else "",
        file.is_source,
        is_external_path(file.owner.workspace_root),
    )

def _strip_root_exec_path_fragment(path, root_fragment):
    if root_fragment and path.startswith(root_fragment + "/"):
        return path[len(root_fragment + "/"):]
    return path

def _strip_external_workspace_prefix(path):
    if is_external_path(path):
        without_prefix = path.split("/", 2)
        if len(without_prefix) > 2:
            return without_prefix[2]
        else:
            return ""
    return path

def to_file_location(exec_path, root_exec_path_fragment, is_source, is_external):
    # directory structure:
    # exec_path = (../repo_name)? + (root_fragment)? + relative_path
    relative_path = _strip_external_workspace_prefix(exec_path)
    relative_path = _strip_root_exec_path_fragment(relative_path, root_exec_path_fragment)

    root_exec_path_fragment = exec_path[:-(len("/" + relative_path))] if relative_path != "" else exec_path

    external_repo_name = None
    if is_external_path(root_exec_path_fragment):
        external_repo_name = root_exec_path_fragment.split("/")[1]

    for repo in REPO_MAPPING.keys():
        if external_repo_name == repo:
            is_external = False
            root_exec_path_fragment = None
            relative_path = REPO_MAPPING[external_repo_name] + "/" + relative_path
            break

    return create_file_location(relative_path, is_source, is_external, root_exec_path_fragment)

def create_file_location(relative_path, is_source, is_external, root_execution_path_fragment):
    return struct(
        relative_path = relative_path,
        is_source = is_source,
        is_external = is_external,
        root_execution_path_fragment = root_execution_path_fragment,
    )

def create_struct(**kwargs):
    d = {name: kwargs[name] for name in kwargs if kwargs[name] != None}
    return struct(**d)

def update_sync_output_groups(groups_dict, key, new_set):
    """Updates all sync-relevant output groups associated with 'key'.

    [key] contains the output group itself and all transitive deps, whereas [key]-direct-outputs contains only direct outputs without deps

    Args:
      groups_dict: the output groups dict, from group name to artifact depset.
      key: the base output group name.
      new_set: a depset of artifacts to add to the output groups.
    """
    update_set_in_dict(groups_dict, key, new_set)
    update_set_in_dict(groups_dict, key + "-direct-outputs", new_set)

def update_set_in_dict(input_dict, key, other_set):
    """Updates depset in dict, merging it with another depset."""
    input_dict[key] = depset(transitive = [input_dict.get(key, depset()), other_set])

def get_aspect_ids(ctx, target):
    """Returns the all aspect ids, filtering out self."""
    aspect_ids = None
    if hasattr(ctx, "aspect_ids"):
        aspect_ids = ctx.aspect_ids
    elif hasattr(target, "aspect_ids"):
        aspect_ids = target.aspect_ids
    else:
        return None
    return [aspect_id for aspect_id in aspect_ids if "bsp_target_info_aspect" not in aspect_id]

REPO_MAPPING = ${repoMapping}

def is_external(target):
    for repo in REPO_MAPPING.keys():
        if target.label.repo_name == repo:
            return False
    return not str(target.label).startswith("@@//") and not str(target.label).startswith("@//") and not str(target.label).startswith("//")

def convert_struct_to_dict(s):
    attrs = dir(s)

    # two deprecated methods of struct
    if "to_json" in attrs:
        attrs.remove("to_json")
    if "to_proto" in attrs:
        attrs.remove("to_proto")

    return {key: getattr(s, key) for key in attrs}

def log(text, level):
    print("[" + level + "] " + str(text))

def log_warn(text):
    log(text, "WARN")

def is_valid_aspect_target(target):
    return BspAspectInfo in target

def _collect_target_from_attr(rule_attrs, attr_name, result):
    """Collects the targets from the given attr into the result."""
    if not hasattr(rule_attrs, attr_name):
        return
    attr_value = getattr(rule_attrs, attr_name)
    type_name = type(attr_value)
    if type_name == "Target":
        result.append(attr_value)
    elif type_name == "list":
        result.extend(attr_value)

def collect_targets_from_attrs(rule_attrs, attrs):
    result = []
    for attr_name in attrs:
        _collect_target_from_attr(rule_attrs, attr_name, result)
    return [target for target in result if is_valid_aspect_target(target)]

def do_starlark_string_expansion_dict(ctx, name, strings_dict, extra_targets = [], should_expand_make_variables = False):
    # first, expand all starlark predefined paths:
    #   location, locations, rootpath, rootpaths, execpath, execpaths
    strings_dict = {k: ctx.expand_location(v, targets = extra_targets) for k, v in strings_dict.items()}

    # then expand any regular GNU make style variables
    if should_expand_make_variables:
        strings_dict = {k: expand_make_variables(v, ctx) for k, v in strings_dict.items()}
    return strings_dict

def do_starlark_string_expansion(ctx, name, strings, extra_targets = [], should_expand_make_variables = False):
    # first, expand all starlark predefined paths:
    #   location, locations, rootpath, rootpaths, execpath, execpaths
    strings = [ctx.expand_location(value, targets = extra_targets) for value in strings]

    # then expand any regular GNU make style variables
    if should_expand_make_variables:
        strings = [expand_make_variables(value, ctx) for value in strings]
    return strings

def expand_make_variables(expression, ctx):
    return _expand_make_variables(ctx, False, [expression])[0]

def files_to_list(source):
    if not hasattr(source, "files"):
        return []
    return source.files.to_list()

def _is_valid_make_var(varname):
    """Check if the make variable name seems valid."""
    if len(varname) == 0:
        return False

    # According to gnu make, any chars not whitespace, ':', '#', '=' are valid.
    invalid_chars = ":#= \t\n\r"
    for n in range(0, len(invalid_chars)):
        if invalid_chars[n] in varname:
            return False
    return True

def map_with_resolve_files(f, xs):
    results = []
    resolve_files = []

    for x in xs:
        if x != None:
            res = f(x)
            if res != None:
                a, b = res
                if a != None:
                    results.append(a)
                if b != None:
                    resolve_files += b

    return results, resolve_files


IMPORT_RULE_KIND = ["java_import", "jvm_import", "kt_jvm_import"]

EXPORT_DEPS = [
    "exports",
]

COMPILE_DEPS = [
    "deps",
    "jars",
    "associates",
    "embed", # from rules_go
    "compilers",  # From go_proto_library
    "java_lib",  # From old proto_library rules
    "proc_macro_deps"
]

PRIVATE_COMPILE_DEPS = [
    "_java_toolchain",
    "_jvm",
    "runtime_jdk",
]

RUNTIME_DEPS = [
    "runtime_deps",
]

ALL_DEPS = COMPILE_DEPS + PRIVATE_COMPILE_DEPS + RUNTIME_DEPS + EXPORT_DEPS
